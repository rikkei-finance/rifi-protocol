#!/usr/bin/env yarn repl -s

-- This script tests a hypothetical upgrade with manual RIFI claims and manual RIFI speed setting

PrintTransactionLogs
Alias RifiHolder "0xC89b6f0146642688bb254bF93C28fcCF1E182C81"
Alias USDCWhale "0x3dfd23a6c5e8bbcfc9581d2e864a68feb6a076d3"
Alias DAIWhale "0x47ac0Fb4F2D84898e4D9E7b4DaB3C24507a6D503"
Web3Fork "https://mainnet-eth.rikkei.finance/@11458477" (RifiHolder USDCWhale DAIWhale)
UseConfigs mainnet

-- Deploy the flywheel impl
CointrollerImpl Deploy Standard CointrollerG7

-- Baseline USDC rifi speed before the patch
Assert Equal (Cointroller RifiSpeed rUSDC) 5.3786477206671197e16

-- Mint tokens
From DAIWhale (Trx GasPrice 0 (Bep20 DAI Approve rDAI UInt256Max))
From DAIWhale (Trx GasPrice 0 (RToken rDAI Mint 10000e6))
From USDCWhale (Trx GasPrice 0 (Bep20 USDC Approve rUSDC UInt256Max))
From USDCWhale (Trx GasPrice 0 (RToken rUSDC Mint 10000e6))

-- Baseline RIFI claim speed
Assert Equal (Cointroller CheckIsRified rDAI) True

Cointroller ClaimRifi DAIWhale
AdvanceBlocks 1000
Expect Changes (Bep20 Rifi TokenBalance DAIWhale) 7.77e2
Cointroller ClaimRifi DAIWhale

-- Propose to apply the patch

From RifiHolder (Rifi Delegate RifiHolder)
From RifiHolder (Governor GovernorAlpha Propose "Disable automatic rifi speed refresh and automatic claims and change rifi speed" [(Address Unitroller) (Address CointrollerG7) (Address Unitroller)] [0 0 0] ["_setPendingImplementation(address)" "_become(address)" "_setRifiSpeed(address,uint256)"] [[(Address CointrollerG7)] [(Address Unitroller)] [(Address rUSDC) 30000000000000000]])

-- Vote for, queue, and execute the proposal

MineBlock
From RifiHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute
CointrollerImpl CointrollerG7 MergeABI

-- Check that speed has changed
Assert Equal (Cointroller RifiSpeed rUSDC) 3.0e16

-- Check that RIFI is still accruing at the same speed
Cointroller ClaimRifi DAIWhale
AdvanceBlocks 1000
Expect Changes (Bep20 Rifi TokenBalance DAIWhale) 7.77e2
Cointroller ClaimRifi DAIWhale

-- Check that minting some USDC wont result in any automatic claims
AdvanceBlocks 1000000
From USDCWhale (Trx GasPrice 0 (Bep20 USDC Approve rUSDC UInt256Max))
Expect Changes (Bep20 Rifi TokenBalance USDCWhale) 0
From USDCWhale (Trx GasPrice 0 (RToken rUSDC Mint 10000e6))

-- Prepare second proposal
From RifiHolder (Governor GovernorAlpha Propose "Remove USDC market by setting rifi speed to 0" [(Address Unitroller)] [0] ["_setRifiSpeed(address,uint256)"] [[(Address rUSDC) 0]])

-- Vote for, queue, and execute the second proposal

MineBlock
From RifiHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute

AdvanceBlocks 1000

-- Check speed is now 0
Assert Equal (Cointroller RifiSpeed rUSDC) 0

-- True up balances
Cointroller ClaimRifi USDCWhale

-- Check no more rifi accrued
AdvanceBlocks 1000000
Expect Changes (Bep20 Rifi TokenBalance USDCWhale) 0
Cointroller ClaimRifi USDCWhale

-- Prepare final proposal
From RifiHolder (Governor GovernorAlpha Propose "Reset USDC rifi speed" [(Address Unitroller)] [0] ["_setRifiSpeed(address,uint256)"] [[(Address rUSDC) 27000000000000000]])

-- Vote for, queue, and execute the final proposal

MineBlock
From RifiHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute

-- Check new speed
Assert Equal (Cointroller RifiSpeed rUSDC) 2.7e16

Print "Upgrade OK!"
