
exchangeRateDecreases(uint result, address minter, uint256 mintAmount, uint256 mintTokens) {
    // Pre/action/post environments
    env e0;
    env e1;
    env e2;

    require e1.block.number >= e0.block.number;
    require e2.block.number >= e1.block.number;

    // Any other account
    address other;
    require other != minter && other != currentContract;
    require minter != currentContract;

    uint256 exchangeRatePre = sinvoke exchangeRateCurrent(e0);

    uint256 rTokenCashPre = sinvoke getCash(e0);
    uint256 rTokenBorrowsPre = sinvoke totalBorrows(e0);
    uint256 rTokenTokensPre = sinvoke totalSupply(e0);
    uint256 rTokenReservesPre = sinvoke totalReserves(e0);
    uint256 rTokenSupplyPre = rTokenCashPre + rTokenBorrowsPre - rTokenReservesPre;

    // Simplifying assumptions to analyze the vulnerability
    require rTokenBorrowsPre == 0; // XXX not necessary, simplifies analysis
    require rTokenSupplyPre >= 0; // XXX not necessary, should underflow
    require rTokenTokensPre > 0; // XXX not necessary
    require exchangeRatePre * rTokenTokensPre == rTokenSupplyPre * 1000000000000000000; // XXX

    // XXX why does this have to be after require?
    uint256 implicitExchangeRatePre = rTokenSupplyPre * 1000000000000000000 / rTokenTokensPre;

    require result == invoke mintFreshPub(e1, minter, mintAmount);
    bool mintFreshReverted = lastReverted;

    uint256 exchangeRatePost = sinvoke exchangeRateCurrent(e2);

    uint256 rTokenCashPost = sinvoke getCash(e2);
    uint256 rTokenBorrowsPost = sinvoke totalBorrows(e2);
    uint256 rTokenTokensPost = sinvoke totalSupply(e2);
    uint256 rTokenReservesPost = sinvoke totalReserves(e2);
    uint256 rTokenSupplyPost = rTokenCashPost + rTokenBorrowsPost - rTokenReservesPost;

    require mintTokens == rTokenTokensPost - rTokenTokensPre;
    require exchangeRatePre * mintTokens == mintAmount * 1000000000000000000;  // XXX why would this need to be assumed? should be proven

    uint256 implicitExchangeRatePost = rTokenSupplyPost * 1000000000000000000 / rTokenTokensPost;

    assert (!mintFreshReverted =>
            ((result != 0) => (exchangeRatePost == exchangeRatePre))), "Mismatch in failure case";
    assert (!mintFreshReverted => (exchangeRatePost >= exchangeRatePre)), "Exchange rate decreased";
    assert (!mintFreshReverted => (implicitExchangeRatePost >= implicitExchangeRatePre)), "Implicit exchange rate decreased";
}
